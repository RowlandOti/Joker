apply plugin: 'com.android.application'

android {
    compileSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId project.APPLICATION_ID
        versionName project.VERSION_NAME
        versionCode Integer.parseInt(project.VERSION_CODE)
        minSdkVersion Integer.parseInt(project.ANDROID_BUILD_MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(project.ANDROID_BUILD_TARGET_SDK_VERSION)
        multiDexEnabled true
        testInstrumentationRunner "android.test.InstrumentationTestRunner"
    }
    signingConfigs {
        release {
            def propFile = file('../.signing/signing.properties')
            if (propFile.exists()) {
                def props = new Properties()
                props.load(new FileInputStream(propFile))

                storeFile = file(props['STORE_FILE'])
                storePassword = props['STORE_PASSWORD']
                keyAlias = props['KEY_ALIAS']
                keyPassword = props['KEY_PASSWORD']
            }
        }
    }
    productFlavors {
        //Paid Version
        paid {
            applicationId = project.APPLICATION_ID+".paid"
            versionName project.VERSION_NAME + "-pro"
            //Accessed in java code as BuildConfig.IS_PRO_VERSION
            buildConfigField "boolean", "IS_PRO_VERSION", "true"
            productFlavors.paid.signingConfig signingConfigs.release

        }
        //Free Version
        free {
            applicationId = project.APPLICATION_ID+".free"
            versionName project.VERSION_NAME + "-free"
            //Accessed in java code as BuildConfig.IS_PRO_VERSION
            buildConfigField "boolean", "IS_PRO_VERSION", "false"
            productFlavors.free.signingConfig signingConfigs.release

        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            // Show the logs when debugging
            buildConfigField "Boolean", "IS_DEBUG_MODE", "true"
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            // Hide all the logs on product release
            buildConfigField "Boolean", "IS_DEBUG_MODE", "false"
        }
    }

    sourceSets {
        test {
            java.srcDir file('src/androidTest/java')
        }
        androidTest {
            java.srcDir file('src/test/java')
        }
    }
}

dependencies {
    // LOCAL BINARY DEPENDENCY
    compile fileTree(dir: 'libs', include: ['*.jar'])
    // TEST BINARY DEPENDENCIES
    testCompile 'junit:junit:4.12'
    // MODULE DEPENDENCY
    compile project(':libraries:common')
    compile project(':libraries:android')
    compile project(path: ':libraries:backend', configuration: 'android-endpoints')
    // REMOTE BINARY DEPENDENCY - Android/Google
    compile 'com.android.support:appcompat-v7:23.1.1'
    // Use - AdMob support
    freeCompile 'com.google.android.gms:play-services:7.3.0'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    androidTestCompile('com.android.support.test:runner:0.3') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test:rules:0.3') {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    compile 'com.jakewharton:butterknife:7.0.1'
}

task androidTest(type: Test, dependsOn: assemble) {

}
check.dependsOn androidTest